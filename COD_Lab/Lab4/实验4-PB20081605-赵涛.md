# 计算机组成原理  实验报告 

> 姓名：赵涛  
>
> 学号：PB20081605  
>
> 实验日期：2022-4-27



### 一、实验题目

​	Lab4  单周期CPU设计



### 二、实验目的：

1. 理解CPU的结构和工作原理。
2. 掌握单周期CPU的设计和调试方法。
3. 熟练掌握数据通路和控制器的设计和描述方法。



### 三、实验环境：

- PC 一台 
- Rars：RISC-V Assembler and Runtime Simulator 
- Vivado
- FPGAOL平台



### 四、实验过程：

	##### 	1.修改Lab2寄存器堆模块，增加1个用于调试的读端口，且使其r0内容恒定为0。寄存器堆代码如下所示：

```verilog
module reg_file(
input clk,
input [4:0] a1,a2,a3, a_debug,
input [31:0] wd3,
input we3,
output [31 :0] rd1 , rd2, rd_debug);

reg [31:0] reg_file[0:31];

integer i;
initial
begin
	for(i=0;i<32;i=i+1) reg_file[i] = 0;
end

always@ (posedge clk)
begin
	if((we3)&&(|a3))		//写有效，且地址不为0
		reg_file[a3] <= wd3;
end
	assign rd1 = a1 ? reg_file[a1] : 32'h0;
	assign rd2 = a2 ? reg_file[a2] : 32'h0;
	assign rd_debug = a_debug ? reg_file[a_debug] : 32'h0;
endmodule
```



		##### 2.其余各个部件代码如下

​	**alu:**

```verilog
module alu(
input [31:0] a,b,
input [3:0] alu_ctrl,
output reg [31:0] alu_out,
output reg zero);

wire signed [31:0] signed_a;
wire signed [31:0] signed_b;

always@ (*)
begin
	case (alu_ctrl)
	4'h0: begin
		alu_out = 32'h0;
		zero = 0;
	end
	4'h1: begin
		alu_out=a+b;
		zero = 0;
	end
	4'h2: begin
		alu_out=a-b;
		zero = 0;
	end
	4'h3: begin
		alu_out = b;
		zero = 0;
	end
	4'h4: begin
		zero = (a < b) ? 1 : 0;	//blt
		alu_out=a+b;
	end
	4'h5: begin
		zero = (a == b)? 1: 0;	//beq
		alu_out=a+b;
	end
	4'h6: begin
		zero = 1;
		alu_out = 32'h0;
	end
	default:begin
		alu_out = 32'h0;
		zero = 0;
	end
	endcase
end

endmodule
```



​		**control:**

```verilog
module control(
    input [6:0] opcode, //Instruction[6:0]

    output reg branch,
    output reg [1:0] MemtoReg,
    output reg [3:0] ALUop,
    output reg MemWrite,
    output reg ALUScr,
    output reg RegWrite,
    output reg jalr
);
    always @(*) begin
        case (opcode)
            //addi
            7'b0010011:
            begin
                branch = 0;
                ALUScr = 1;
                ALUop = 4'h1;   //+
                MemWrite = 0;
                MemtoReg = 2'b01;
                RegWrite = 1;
                jalr = 0;
                //MemRead = 0;
            end

            //add / sub
            7'b0110011:
            begin
                branch = 0;
                ALUScr = 0;
                ALUop = 4'h2;   //-
                //add 为 此处为 - 4'h1
                //用 ALU control 解决
                MemWrite = 0;
                MemtoReg = 2'b01;
                RegWrite = 1;
                jalr = 0;
            end

            //auipc
            7'b0010111:
            begin
                branch = 1;
                ALUScr = 1;
                ALUop = 4'h3;
                MemWrite = 0;
                MemtoReg = 2'b11;
                RegWrite = 1;
                jalr = 0;
            end

            //lw
            7'b0000011:
            begin
                branch = 0;
                ALUScr = 1;
                ALUop = 4'h1;   //+
                MemWrite = 0;
                MemtoReg = 2'b00;
                RegWrite = 1;
                jalr = 0;
            end

            //sw
            7'b0100011:
            begin
                branch = 0;
                ALUScr = 1;
                ALUop = 4'h1;   //+
                MemWrite = 1;
                MemtoReg = 2'b00;
                RegWrite = 0;
                jalr = 0;
            end

            //beq / blt
            7'b1100011:
            begin
                branch = 1;
                ALUScr = 0;
                ALUop = 4'h4;   //blt
                //beq 此处应为 4'h5
                MemWrite = 0;
                MemtoReg = 2'b00;
                RegWrite = 0;
                jalr = 0;
            end

            //jal
            7'b1101111:
            begin
                branch = 1;
                ALUScr = 0; //无影响
                ALUop = 4'h6;
                MemWrite = 0;
                MemtoReg = 2'b10;   //reg = pc + 4
                RegWrite = 1;
                jalr = 0;
            end

            //jalr
            7'b1100111:
            begin
                branch = 1; 
                ALUScr = 1;
                ALUop = 4'h1;
                MemWrite = 0;
                MemtoReg = 2'b10;
                RegWrite = 1;
                jalr = 1;
            end
            
            default: ;
        endcase
    end
endmodule
```



​		**立即数拓展模块imm:**

```verilog
module imm(
input [31:0] inst,
output reg [31:0] imm_out
);

wire [6:0] inst_type;
assign inst_type = inst[6:0];

always@ (*)
begin
	case (inst_type)
	7'h03: imm_out = {{21{inst[31]}} ,inst[30:20]} ;	//i type:lb、 lh、1w、lbu、 lhu
	7'h13: imm_out = {{21{inst[31]}} , inst[30:20]} ;	//i type logic and i type shift
	7'h17: imm_out = {inst[31:12] ,12'h0} ;				//auipc
	7'h37: imm_out = {inst[31:12] ,12'h0} ;				//lui
	7'h6f: imm_out = {{13{inst[31]}} ,inst[19:12] , inst[20] , inst[30:21],1'b0};		//jal
	7'h63: imm_out = {{20{inst[31]}} ,inst[7] ,inst[30:25] ,inst[11:8] ,1'b0};			//b_type
	7'h23: imm_out = {{21{inst[31]}} ,inst[30:25] , inst[11:7]};						//s_type
	7'h67: imm_out = {{21{inst[31]}} ,inst[30:20]} ;	//jalr
	default: imm_out = 32'h0;
	endcase
end

endmodule
```



​		**控制pc的模块pc_calc:**

```verilog
module pc_calc(
input clk,
input rst,
input branch,
input [31:0] alu_out,
input jalr,
input [31:0] wd,
output reg [31:0] pc,
output [31:0] pc_plus4);
always@ (posedge clk or posedge rst)
begin
	if (rst)
		pc <= 32'h0000_3000;
	else if (branch)
		pc <= alu_out;
	else if(jalr)
		pc <= {wd[31:1], 1'b0};
	else
		pc <= pc_plus4;
	end

	assign pc_plus4 = pc + 32'h4;
endmodule
```



​		**cpu模块**

```verilog
`timescale 1ns / 1ps

module  cpu (
  input clk, 
  input rst,

  //IO_BUS
  output [7:0] io_addr,      //led和seg的地址
  output [31:0] io_dout,     //输出led和seg的数据
  output io_we,                 //输出led和seg数据时的使能信号
  input [31:0] io_din,          //来自sw的输入数据

  //Debug_BUS
  input [7:0] m_rf_addr,   //存储器(MEM)或寄存器堆(RF)的调试读口地址
  output [31:0] rf_data,    //从RF读取的数据
  output [31:0] m_data,    //从MEM读取的数据
  output [31:0] pc             //PC的内容
);

wire branch;
wire [1:0] MemtoReg;
wire [3:0] ALUop;
wire MemWrite;
wire ALUScr;
wire RegWrite;
wire jalr;

wire [31:0] pc_a;
wire [31:0] pc_plus4;
wire [31:0] ins;
wire [31:0] wd;
wire [31:0] rd1;
wire [31:0] rd2;
wire [31:0] imm32;
wire [31:0] alu_res;
wire zero;
wire [31:0] read_data;
wire mux_2_ctrl;
wire [31:0] pc_plus_offset;
wire [4:0] a_debug;
wire [31:0] rd_debug;
wire [31:0] rd2_mux;
wire [3:0] alu_ctrl;

assign mux_2_ctrl = branch & zero;
assign pc_plus_offset = pc_a + imm32;
assign a_debug = m_rf_addr[4:0];

assign pc = pc_a;

assign rf_data = rd_debug;
assign io_we = 0;
assign io_dout = 32'h00000000;
assign io_addr = 8'h0000;

wire [31:0] d;
assign d = 32'h00000000;
wire we;
assign we = 0;

  pc_calc pc_calc(.clk(clk), .rst(rst), .branch(mux_2_ctrl), .alu_out(pc_plus_offset), .jalr(jalr), .wd(alu_res),
                   .pc(pc_a), .pc_plus4(pc_plus4));
  dist_ins_mem dist_ins_mem(.a(pc_a[9:2]), .d(d), .clk(clk), .we(we), .spo(ins));
  control control(.opcode(ins[6:0]), .branch(branch), .MemtoReg(MemtoReg),
                   .ALUop(ALUop), .MemWrite(MemWrite), .ALUScr(ALUScr), .RegWrite(RegWrite), .jalr(jalr));
  reg_file reg_file(.clk(clk), .a1(ins[19:15]), .a2(ins[24:20]), .a3(ins[11:7]), .a_debug(a_debug),
                    .wd3(wd), .we3(RegWrite), .rd1(rd1), .rd2(rd2), .rd_debug(rd_debug));
  imm imm(.inst(ins), .imm_out(imm32));
  ALU_control ALU_control(.ins30({ins[30], ins[14:12]}), .ALUop(ALUop), .alu_ctrl(alu_ctrl));
  alu alu(.a(rd1), .b(rd2_mux), .alu_ctrl(alu_ctrl), .alu_out(alu_res), .zero(zero));
  MUX2 MUX2_1(.sel(ALUScr), .input1(rd2), .input2(imm32), .output_a(rd2_mux));
  dist_data_mem dist_data_mem(.a(alu_res[9:2]), .d(rd2), .dpra(m_rf_addr), .clk(clk), .we(MemWrite), .spo(read_data), .dpo(m_data));
  MUX3 MUX3_2(.sel(MemtoReg), .input1(read_data), .input2(alu_res), .input3(pc_plus4), .input4(pc_plus_offset),.output_a(wd));

endmodule


module ALU_control (
  input [3:0] ins30,
  input [3:0] ALUop,
  output reg [3:0] alu_ctrl
);

  always @(*) begin
    if(ALUop == 4'h2)
      alu_ctrl = ins30[3] ? ALUop : 4'h1;
    else if(ALUop == 4'h4)
      alu_ctrl = ins30[2] ? ALUop : 4'h5; 
    else
      alu_ctrl = ALUop;
  end
  
endmodule

module MUX2 (
  input sel,
  input [31:0] input1,
  input [31:0] input2,
  output [31:0] output_a
);
  assign output_a = sel ? input2 : input1;
  
endmodule

module MUX3 (
  input [1:0] sel,
  input [31:0] input1, input2, input3, input4,
  output reg [31:0] output_a
);

  always @(*) begin
    if(sel == 2'b00)
      output_a = input1;
    else if(sel == 2'b01)
      output_a = input2;
    else if(sel == 2'b10)
      output_a = input3;
    else
      output_a = input4;
  end
  
endmodule
```



​		**pdu模块(对老师的pdu做了修改，pdu充当top的功能，因此不必再多写一个top模块）：**

```verilog
`timescale 1ns / 1ps

module  pdu_1cycle(
  input clk,
  input rst,

  //选择CPU工作方式;
  input run, 
  input step,

  //输入switch的端口
  input valid,
  input [4:0] in,

  //输出led和seg的端口 
  output [1:0] check,  //led6-5:查看类型
  output [4:0] out0,    //led4-0
  output [2:0] an,     //8个数码管
  output [3:0] seg,
  output ready          //led7
);

  wire clk_cpu;

  //IO_BUS
  wire [7:0] io_addr;
  wire [31:0] io_dout;
  wire io_we;
  wire [31:0] io_din;

  //Debug_BUS
  wire [7:0] m_rf_addr;
  wire [31:0] rf_data;
  wire [31:0] m_data;
  wire [31:0] pc;

    cpu cpu(.clk(clk_cpu), .rst(rst), .io_addr(io_addr), .io_dout(io_dout),
            .io_we(io_we), .io_din(io_din), .m_rf_addr(m_rf_addr),
            .rf_data(rf_data), .m_data(m_data), .pc(pc));



reg [4:0] in_r;    //同步外部输入用
reg run_r, step_r, step_2r, valid_r, valid_2r;
wire step_p, valid_pn;  //取边沿信号

reg clk_cpu_r;      //寄存器输出CPU时钟
reg [4:0] out0_r;   //输出外设端口
reg [31:0] out1_r;
reg ready_r;
reg [19:0] cnt;     //刷新计数器，刷新频率约为95Hz
reg [1:0] check_r;  //查看信息类型, 00-运行结果，01-寄存器堆，10-存储器，11-PC

reg [7:0] io_din_a; //_a表示为满足组合always描述要求定义的，下同
reg ready_a;
reg [4:0] out0_a;
reg [31:0] out1_a;
reg [3:0] seg_a;

assign clk_cpu = clk_cpu_r;
assign io_din = io_din_a;
assign check = check_r;
assign out0 = out0_a;
assign ready = ready_a;
assign seg = seg_a;
assign an = cnt[19:17];
assign step_p = step_r & ~step_2r;     //取上升沿
assign valid_pn = valid_r ^ valid_2r;  //取上升沿或下降沿
assign m_rf_addr = {{3{1'b0}}, in_r};

//同步输入信号
always @(posedge clk) begin
  run_r <= run;
  step_r <= step;
  step_2r <= step_r;
  valid_r <= valid;
  valid_2r <= valid_r;
  in_r <= in;           
end

//仿真需要
initial begin
  run_r = 1;
end

//CPU工作方式
always @(posedge clk, posedge rst) begin
  if(rst)
    clk_cpu_r <= 0;
  else if (run_r)
    clk_cpu_r <= ~clk_cpu_r;
  else
    clk_cpu_r <= step_p;
end

//TODO:
//读外设端口
always @* begin
  case (io_addr)
    8'h0c: io_din_a = {{27{1'b0}}, in_r};
    8'h10: io_din_a = {{31{1'b0}}, valid_r};
    default: io_din_a = 32'h0000_0000;
  endcase
end

//写外设端口
always @(posedge clk, posedge rst) begin
if (rst) begin
  out0_r <= 5'h1f;
  out1_r <= 32'h1234_5678;
  ready_r <= 1'b1;
end
else if (io_we)
  case (io_addr)
    8'h00: out0_r <= io_dout[4:0];
    8'h04: ready_r <= io_dout[0];
    8'h08: out1_r <= io_dout;
    default: ;
  endcase
end

//LED和数码管查看类型
always @(posedge clk, posedge rst) begin
if(rst)
    check_r <= 2'b00;            
  else if(run_r)
    check_r <= 2'b00;
  else if (step_p)
    check_r <= 2'b00;
  else if (valid_pn)
    check_r <= check - 2'b01;
end

//LED和数码管显示内容
always @* begin
  ready_a = 1'b0;
  case (check_r)
    2'b00: begin
      out0_a = out0_r;
      out1_a = out1_r;
      ready_a = ready_r; 
    end
    2'b01: begin
      out0_a = in_r;
      out1_a = rf_data;
    end
    2'b10: begin
      out0_a = in_r;
      out1_a = m_data;
    end
    2'b11: begin
      out0_a = 5'b00000;
      out1_a = pc;
    end
  endcase
end

//扫描数码管
always @(posedge clk, posedge rst) begin
  if (rst) cnt <= 20'h0_0000;
  else cnt <= cnt + 20'h0_0001;
end

always @* begin
  case (an)
    3'd0: seg_a = out1_a[3:0];
    3'd1: seg_a = out1_a[7:4];
    3'd2: seg_a = out1_a[11:8];
    3'd3: seg_a = out1_a[15:12];
    3'd4: seg_a = out1_a[19:16];
    3'd5: seg_a = out1_a[23:20];
    3'd6: seg_a = out1_a[27:24];
    3'd7: seg_a = out1_a[31:28];
    default: ;
  endcase
end

endmodule
```



​		**存储器：（ip核实现）**

​		指令存储器dist_ins_mem的veo文件：

```verilog
dist_ins_mem your_instance_name (
  .a(a),      // input wire [7 : 0] a
  .d(d),      // input wire [31 : 0] d
  .clk(clk),  // input wire clk
  .we(we),    // input wire we
  .spo(spo)  // output wire [31 : 0] spo
);
```



​		数据存储器dist_data_mem的veo文件：

```verilog
dist_data_mem your_instance_name (
  .a(a),        // input wire [7 : 0] a
  .d(d),        // input wire [31 : 0] d
  .dpra(dpra),  // input wire [7 : 0] dpra
  .clk(clk),    // input wire clk
  .we(we),      // input wire we
  .spo(spo),    // output wire [31 : 0] spo
  .dpo(dpo)    // output wire [31 : 0] dpo
);
```



##### 3.通过查看RTL文件，检查数据通路连接是否与设想的一致。



##### 4.设计用于测试cpu功能正确性的汇编程序，其代码如下所示：

```as
.data
out: .word 0x00 	#led, 初始全灭
data: .word 0x12
in: .word 0 		#switch

.text
la a0, out			#仿真需要

addi t0, x0, 0xff 	#test addi
sw t0, 0(a0)		#test sw

sw x0, 0(a0) 		#test sw

addi t0, x0, 0x0f
sw t0, 0(a0)

add t3, t0, t0		#test add
sw t3, 0(a0)

sub t1, t3, t0		#test sub
sw t1, 0(a0)

lw t0, 4(a0) 		#test lw
sw t0, 0(a0)	

addi t2, x0, 0x0f

beq t1, t2, show2	#test beq:全灭led
show1:
addi t0, x0, 0xff
sw t0, 0(a0)
show2:
sw x0, 0(a0)

blt t2, t3, show4	#test blt:全亮les
show3:
sw x0, 0(a0)
show4:
addi t0, x0, 0xff
sw t0, 0(a0)

auipc t6, 0x01		#test auipc
sw t6, 0(a0)

jal t4, show5		#test jal
sub t0, t3, t2
show5:
sw t0, 0(a0)

jalr t5, 1(t4)		#test jalr

addi t0, x0, 0x0f
sw t0, 0(a0)
```



##### 5.生成COE文件，并利用coe文件对cpu内的存储器进行初始化。coe文件如下所示：

​	ins:

```coe
memory_initialization_radix=16;
memory_initialization_vector=ffffd517 00050513 0ff00293 00552023 00052023 00f00293 00552023 00528e33 01c52023 405e0333 00652023 00452283 00552023 00f00393 00730663 0ff00293 00552023 00052023 01c3c463 00052023 0ff00293 00552023 00001f97 01f52023 00800eef 407e02b3 00552023 001e8f67 00f00293 00552023;
```

​	data:

```coe
memory_initialization_radix=16;
memory_initialization_vector=00000000 00000012 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 ......(之后全为00000000)
```



##### 6.对cpu进行仿真（实际完成过程中，是直接对top进行仿真，然后检查内部cpu的信号），检验结果的正确性。

##### 	仿真文件代码如下所示：

```verilog
`timescale 1ns / 1ps

module pdu_sim();

  reg clk;
  reg rst;

  //选择CPU工作方式;
  reg run; 
  reg step;
  //wire clk_cpu;

  //输入switch的端口
  reg valid;
  reg [4:0] in;

  //输出led和seg的端口 
  wire [1:0] check;  //led6-5:查看类型
  wire [4:0] out0;    //led4-0
  wire [2:0] an;     //8个数码管
  wire [3:0] seg;
  wire ready;          //led7


  pdu_1cycle pdu_1cycle(.clk(clk), .rst(rst), .run(run), .step(step), 
                        .valid(valid), .in(in), .check(check),
                        .out0(out0), .an(an), .seg(seg), .ready(ready));

integer i;

    initial begin
        run = 1;
        step = 0;
        in = 5'h00;
        //check = 2'b00;
        rst = 1; #1;
        rst = 0;
    end

    initial begin
        clk = 0;
        for(i = 0; i < 120; i = i + 1)
            #1 clk = ~clk;
        $finish;
    end

endmodule
```



##### 7.将CPU和PDU下载至FPGA中测试，检查结果的正确性。





### **五**、实验结果：

#### cpu的结构：（通过查看RTL电路检查）

![屏幕截图 2022-04-28 151821](D:\OneDrive - USTC\2022spring\COD\Lab\Lab4\截图\屏幕截图 2022-04-28 151821.png)

​		经检验，与设想的一致。



#### cpu的仿真结果：

![仿真1](D:\OneDrive - USTC\2022spring\COD\Lab\Lab4\截图\仿真1.png)

![仿真1](D:\OneDrive - USTC\2022spring\COD\Lab\Lab4\截图\仿真1.png)

![仿真3](D:\OneDrive - USTC\2022spring\COD\Lab\Lab4\截图\仿真3.png)



​	与Rars上运行的结果相对照，经检验，结果无误。



#### FPGA中测试结果

​										<img src="C:\Users\86186\AppData\Roaming\Typora\typora-user-images\image-20220428213541375.png" alt="image-20220428213541375" style="zoom:33%;" />

<img src="C:\Users\86186\AppData\Roaming\Typora\typora-user-images\image-20220428213556711.png" alt="image-20220428213556711" style="zoom:33%;" />

<img src="C:\Users\86186\AppData\Roaming\Typora\typora-user-images\image-20220428213643896.png" alt="image-20220428213643896" style="zoom:33%;" />

​		

​		与Rars上运行的结果相对照，经检验，结果无误。



### **心得体会：**

1. 理解CPU的结构和工作原理。
2. 掌握了单周期CPU的设计和调试方法。
3. 加深了自己对数据通路的理解，熟练掌握了数据通路和控制器的设计和描述方法。



​		

​		